      *---------------------------------------------------------------*
      *                                                               *
      *  *******  **   **  **  *****  **  **                          *
      *  **   **  **   **  **  **     ** **          PRODOTTO         *
      *  **   **  **   **  **  **     ***       ANTICIPO DOCUMENTI    *
      *  ** * **  **   **  **  **     ** **                           *
      *  *******  *******  **  *****  **  **                          *
      *       *                                  INFOSER S.R.L.       *
      *                                                               *
      *---------------------------------------------------------------*
      *  ADW10R    TRASCODIFICA IMPORTI                               *
      *                                                               *
      *---------------------------------------------------------------*
       W0010-RICHIAMO.
      *
           MOVE 'SI'   TO W10-RCODE.
           MOVE SPACES TO WRK-SW-CONTR.
           IF  W10-CAMPO1 EQUAL ZERO
               MOVE 'SI' TO WRK-SW-CONTR
               GO TO W0010-1.
           IF  W10-CAMPO2 NOT EQUAL SPACES
               MOVE 'NO' TO W10-RCODE
               GO TO W0010-FINE.
           MOVE W10-CAMPO1 TO WRK-W-EDIT.
           MOVE WRK-W-EDIT TO W10-CAMPO2.
           GO TO W0010-FINE.
       W0010-1.
           IF W10-CAMPO2 EQUAL SPACES
              MOVE 'NO' TO W10-RCODE
              GO TO W0010-FINE.
           IF W10-LENGH NOT NUMERIC
              MOVE 'NO' TO W10-RCODE
              GO TO W0010-FINE.
           MOVE W10-CAMPO2 TO WRK-W-TAB.
           IF W10-FUNZI EQUAL 'D'
              PERFORM K2000-DECIMAL THRU K2000-END
              GO TO W0010-FINE.
           PERFORM K1000-CONTR  THRU K1000-END.
           IF  W10-RCODE EQUAL 'NO'
               GO TO W0010-FINE.
           PERFORM K1010-SHIFT THRU K1010-END.
           MOVE '+' TO WRK-SEGNO.
           IF  WRK-W-EL (24) EQUAL '-'
               MOVE '-' TO WRK-SEGNO
               MOVE SPACE TO WRK-W-EL (24)
               GO TO W0010-2.
           IF  WRK-W-EL (24) EQUAL '+'
               MOVE SPACE TO WRK-W-EL (24).
       W0010-2.
           PERFORM K1010-SHIFT THRU K1010-END.
           IF  WRK-W-TAB EQUAL ALL '0'
               MOVE 'SI' TO W10-RCODE
               GO TO W0010-FINE.
           PERFORM K1020-TAB THRU K1020-END.
           IF  W10-RCODE EQUAL 'NO'
               GO TO W0010-FINE.
           MOVE '000' TO WRK-W-TAB2.
           IF  WRK-VIRG EQUAL ZERO
               GO TO W0010-4.
           MOVE SPACES TO WRK-W-EL (WRK-I3).
           ADD 1 TO WRK-I3.
           MOVE ZERO TO WRK-I2.
       W0010-3.
           IF  WRK-I3 LESS 25
               ADD 1 TO WRK-I2
               MOVE WRK-W-EL (WRK-I3) TO WRK-W-EL2 (WRK-I2)
               MOVE SPACE TO WRK-W-EL (WRK-I3)
               ADD 1 TO WRK-I3
               GO TO W0010-3.
           PERFORM K1010-SHIFT THRU K1010-END.
       W0010-4.
           PERFORM K1030-PUNT THRU K1030-END.
           IF  W10-RCODE EQUAL 'NO'
               GO TO W0010-FINE.
           IF  WRK-ZERI NOT EQUAL ZERO
      *        MOVE 'NO' TO W10-RCODE
      *        GO TO W0010-FINE.
               COMPUTE W10-CAMPO1R = WRK-W-IMP-18
               IF  WRK-SEGNO EQUAL '-'
                 MULTIPLY W10-CAMPO1R BY -1 GIVING W10-CAMPO1R
               ELSE
                 NEXT SENTENCE
           ELSE
           COMPUTE W10-CAMPO1 = WRK-W-IMP + (WRK-W-TAB2-R / 1000)
           IF  WRK-SEGNO EQUAL '-'
               MULTIPLY W10-CAMPO1 BY -1 GIVING W10-CAMPO1.
       W0010-FINE.
           IF WRK-SW-CONTR EQUAL 'SI'
              IF W10-CAMPO1 GREATER WRK-ELEM-IMP(W10-LENGH)
                 MOVE 'ER'          TO W10-RCODE.
      *
       W0010-END.
           EXIT.
      ******************************************************************
      * 01    CONTROLLO DEL CAMPO EDITATO
      *       CONTROLLA CHE NON CI SIANO BLANKS ALL'INTERNO DEL CAMPO
      *       EDITATO E CHE LO STESSO COMINCI CON UN CARATTERE NUMERICO
      ******************************************************************
       K1000-CONTR.
           MOVE 1 TO WRK-I1.
       K1000-CONTR-1.
           IF  WRK-I1 GREATER 24
               GO TO K1000-END.
           IF  WRK-W-EL (WRK-I1) EQUAL SPACE
               ADD 1 TO WRK-I1
               GO TO K1000-CONTR-1.
           IF  WRK-W-EL (WRK-I1) NOT NUMERIC
               MOVE 'NO' TO W10-RCODE
               GO TO K1000-END.
           ADD 1 TO WRK-I1.
       K1000-CONTR-2.
           IF  WRK-I1 GREATER 24
               GO TO K1000-END.
           IF  WRK-W-EL (WRK-I1) NOT EQUAL SPACE
               ADD 1 TO WRK-I1
               GO TO K1000-CONTR-2.
           ADD 1 TO WRK-I1.
       K1000-CONTR-3.
           IF  WRK-I1 GREATER 24
               GO TO K1000-END.
           IF  WRK-W-EL (WRK-I1) EQUAL SPACE
               ADD 1 TO WRK-I1
               GO TO K1000-CONTR-3.
           MOVE 'NO' TO W10-RCODE.
       K1000-END.
           EXIT.
      ******************************************************************
      * 02    SHIFT DI WRK-W-TAB CON RIEMPIMENTO DI ZERI A SINISTRA
      ******************************************************************
       K1010-SHIFT.
           MOVE 24 TO WRK-I1 WRK-I2.
       K1010-SHIFT-1.
           IF  WRK-I1 EQUAL ZERO
               GO TO K1010-SHIFT-2.
           IF  WRK-W-EL (WRK-I1) EQUAL SPACE
               SUBTRACT 1 FROM WRK-I1
               GO TO K1010-SHIFT-1.
           MOVE WRK-W-EL (WRK-I1) TO WRK-W-EL1 (WRK-I2).
           SUBTRACT 1 FROM WRK-I1 WRK-I2.
           GO TO K1010-SHIFT-1.
       K1010-SHIFT-2.
           IF  WRK-I2 EQUAL ZERO
               MOVE WRK-W-TAB1 TO WRK-W-TAB
               GO TO K1010-END.
           MOVE '0' TO WRK-W-EL1 (WRK-I2).
           SUBTRACT 1 FROM WRK-I2.
           GO TO K1010-SHIFT-2.
       K1010-END.
           EXIT.
      ******************************************************************
      * 03    CONTROLLO CARATTERI DELLA TABELLA
      *       CONTROLLA SE IN WRK-W-TAB CI SONO CARATTERI NON NUMERICI,
      *             CHE ESISTA SOLO UNA VIRGOLA E CHE VI SIANO MASSIMO
      *             TRE DECIMALI.RITORNA LA POSIZIONE DELLA VIRGOLA
      *             NEL CONTATORE WRK-I3.
      ******************************************************************
       K1020-TAB.
           MOVE ZERO TO WRK-VIRG WRK-I3.
           MOVE 1 TO WRK-I1.
       K1020-TAB-1.
           IF  WRK-I1 GREATER 24
               GO TO K1020-TAB-2.
           IF  WRK-W-EL (WRK-I1) EQUAL '.'
               ADD 1 TO WRK-I1
               GO TO K1020-TAB-1.
           IF  WRK-W-EL (WRK-I1) EQUAL ','
               ADD 1 TO WRK-VIRG
               MOVE WRK-I1 TO WRK-I3
               ADD 1 TO WRK-I1
               GO TO K1020-TAB-1.
           IF  WRK-W-EL (WRK-I1) NOT NUMERIC
               MOVE 'NO' TO W10-RCODE
               GO TO K1020-END.
           ADD 1 TO WRK-I1.
           GO TO K1020-TAB-1.
       K1020-TAB-2.
           IF  WRK-VIRG EQUAL ZERO
               GO TO K1020-END.
           IF  WRK-VIRG GREATER 1
               MOVE 'NO' TO W10-RCODE
               GO TO K1020-END.
E01163*    IF  WRK-I3 EQUAL 24 OR LESS 21
E01163     IF  WRK-I3 EQUAL 24 OR LESS 24
               MOVE 'NO' TO W10-RCODE.
       K1020-END.
           EXIT.
      ******************************************************************
      * 04    CONTROLLO PUNTEGGIATURA DEL CAMPO EDITATO
      ******************************************************************
       K1030-PUNT.
           IF  WRK-W-EL (24) EQUAL '.'
               MOVE 'NO' TO W10-RCODE
               GO TO K1030-END.
           MOVE 24 TO WRK-I1.
       K1030-PUNT-1.
           IF  WRK-I1 EQUAL ZERO
               PERFORM K1010-SHIFT THRU K1010-END
               GO TO K1030-END.
           IF  WRK-W-EL (WRK-I1) NOT EQUAL '.'
               SUBTRACT 1 FROM WRK-I1
               GO TO K1030-PUNT-1.
           SUBTRACT WRK-I1 FROM 24 GIVING WRK-I3.
           IF  WRK-I3 EQUAL 3 OR 7 OR 11 OR 15
             NEXT SENTENCE
             ELSE
               MOVE 'NO' TO W10-RCODE
               GO TO K1030-END.
           MOVE SPACE TO WRK-W-EL (WRK-I1).
           SUBTRACT 1 FROM WRK-I1.
           GO TO K1030-PUNT-1.
       K1030-END.
           EXIT.
      ******************************************************************
      * CONTROLLO E RIEMPIMENTO CON ZERI A DESTRA DI UN CAMPO DECIMALE *
      ******************************************************************
       K2000-DECIMAL.
           MOVE ALL '0' TO WRK-W-TAB1.
           MOVE 1 TO WRK-I1.
           MOVE 1 TO WRK-I2.
       K2000-DECIMAL-1.
           IF WRK-W-EL(WRK-I1) NUMERIC
              MOVE WRK-W-EL(WRK-I1) TO WRK-W-EL1(WRK-I2)
              ADD 1 TO WRK-I2.
           IF WRK-W-EL(WRK-I1) NOT NUMERIC
              IF WRK-W-EL(WRK-I1) NOT EQUAL SPACES
                 MOVE 'NO' TO W10-RCODE
                 GO TO K2000-END.
           ADD 1 TO WRK-I1.
           IF WRK-I1 NOT GREATER W10-LENGH
              GO TO K2000-DECIMAL-1.
           MOVE ALL '0' TO WRK-W-TAB.
           MOVE W10-LENGH TO WRK-I1.
           MOVE 24 TO WRK-I2.
       K2000-DECIMAL-2.
           MOVE WRK-W-EL1(WRK-I1) TO WRK-W-EL(WRK-I2).
           SUBTRACT 1 FROM WRK-I1.
           SUBTRACT 1 FROM WRK-I2.
           IF WRK-I1 GREATER ZERO
              GO TO K2000-DECIMAL-2.
           MOVE WRK-W-IMP TO W10-CAMPO1.
       K2000-END.
           EXIT.
      *---------------------------------------------------------------*
